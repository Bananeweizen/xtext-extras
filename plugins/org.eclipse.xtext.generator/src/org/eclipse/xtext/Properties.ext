/*******************************************************************************
 * Copyright (c) 2008 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 *******************************************************************************/
import xtext;
import xtextgen;

extension org::eclipse::xtext::Extensions;

GenModel genModel() :
	GLOBALVAR genModel;

modelFileExtensions() :
	genModel().modelFileExtensions;

fileHeader() :
	genModel().fileHeader;
	
grammar() :
	genModel().grammar;

grammarResourceURI() :
	genModel().grammarResourceURI;

String languageName() :
	grammar().getName().replaceAll("\\s","_");
	
languageId() :
	grammar().getId();
	
languageNamespace() :
	grammar().getNamespace();

toPath(String fqName) :
	fqName.replaceAll("\\.", "/");
	
toJavaFilePath(String fqClassName) :
	toPath(fqClassName) + ".java";
	
toJavaPackage(String fqName) :
	skipLastToken(fqName, '.');
	
toSimpleName(String fqName) :
	lastToken(fqName, '.');

private String skipLastToken(String s, String separator) :
	JAVA org.eclipse.xtext.util.Strings.skipLastToken(java.lang.String, java.lang.String);
	
private String lastToken(String s, String separator) :
	JAVA org.eclipse.xtext.util.Strings.lastToken(java.lang.String, java.lang.String);
	
outletPath(String name) :
	outletPath(genModel(), name);

private String outletPath(GenModel model, String name) :
	JAVA org.eclipse.xtext.GenExtensions.outletPath(org.eclipse.xtext.xtextgen.GenModel, java.lang.String);

Void copyFiles(String srcDir, String targetDir, List[String] fileNames) :
	JAVA org.eclipse.xtext.util.Files.copyFiles(java.lang.String, java.lang.String, java.util.List);

GenService findService(String serviceInterfaceFQName) :
	genModel().services.select(s|s.serviceInterfaceFQName==serviceInterfaceFQName).first();

GenService resourceFactoryService() :
	findService("org.eclipse.xtext.resource.IResourceFactory") != null 
		? findService("org.eclipse.xtext.resource.IResourceFactory")
		: null;
