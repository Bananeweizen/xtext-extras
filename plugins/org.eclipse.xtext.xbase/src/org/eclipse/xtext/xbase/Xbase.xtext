grammar org.eclipse.xtext.xbase.Xbase with org.eclipse.xtext.xbase.Xpression

import "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.ecore"
import "platform:/resource/org.eclipse.xtext.xbase/model/Xpression.ecore" as xpression
import "platform:/resource/org.eclipse.xtext.xbase/model/Xtype.ecore" as xtype

XFile  : 	
    (package=QualifiedName)?
	(imports+=XImport)*
	(classes+=XClass)*
;

XImport  : 
	'import' importedNamespace=XQualifiedNameWithWildCard
;

XQualifiedNameWithWildCard :
	QualifiedName  ('.' '*')?;
	
XClass :
   "class" name=ID ("extends" extends=XTypeRef)? ('implements' implements+=XTypeRef)* '{'
	   (functions+=XFunction)*
   '}'; 

XFunction  : 
	( private?='private')? 
	('<' typeParams+=XTypeParamDeclaration (',' typeParams+=XTypeParamDeclaration )*'>')?
	(returnType=XTypeRef)? name=ID '(' (declaredParams+=XDeclaredParameter (',' declaredParams+=XDeclaredParameter )*)? ')'
			(':' expression=XExpression ';' | expression=XBlockExpression)
;


