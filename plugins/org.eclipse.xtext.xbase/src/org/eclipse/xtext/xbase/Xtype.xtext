/*******************************************************************************
 * Copyright (c) 2010 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
 
grammar org.eclipse.xtext.xbase.Xtype with org.eclipse.xtext.common.Terminals

import "platform:/resource/org.eclipse.xtext.xbase/model/Xtype.ecore"
import "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.ecore" as types

XTypeRef:
  XSimpleTypeRef|XFunctionTypeRef;

XFunctionTypeRef:
  ('(' paramTypes+=XTypeRef (',' paramTypes+=XTypeRef)* ')')? '=>' returnType=XTypeRef;

XSimpleTypeRef:
  type=[types::Type|QualifiedName] ('<' typeParams+=XTypeParam (',' typeParams+=XTypeParam)* '>')?;

XTypeParam returns XTypeRef:
  XTypeRef|XWildcardParam;


XWildcardParam returns XTypeRef:
  {XWildcardParam} '?' 
  (
   ('extends' extends=XTypeRef) |
   ('super' super=XTypeRef)
  )?;

XTypeParamDeclaration:
  name=ID 
  (
   ('extends' extends+=XTypeRef ('&' extends+=XTypeRef)*) |
   ('super' super=XTypeRef)
  )?;

QualifiedName:
  ID ('.' ID)*;