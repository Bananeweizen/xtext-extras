language org.eclipse.xtext.grammarinheritance.ConcreteTestLanguage extends org.eclipse.xtext.grammarinheritance.AbstractTestLanguage

generate foo "http://holla"

import "classpath:/org/eclipse/xtext/grammarinheritance/ametamodel.ecore" as mm

RootRule:
	ConcreteParserRule | CallOverridenParserRule | CallExtendedParserRule | OverridableParserRule2;

// call InheritedParserRule
ConcreteParserRule :
	'model' magicNumber=REAL ':' (elements+=InheritedParserRule)*;
	
// override OverridableParserRule and call it
OverridableParserRule returns mm::AType:
	'overriddenelement' name=ID;
	
CallOverridenParserRule:
	call=AbstractCallOverridenParserRule;
	
// override parser rule and change return type
OverridableParserRule2 returns AType2 :
	'overridden other element' name=ID '-' age=INT;
	
// override and extend ExtendableParserRule and call it
ExtendableParserRule returns mm::AType:
	Subrule1 | Subrule2 | Subrule3;
	
Subrule1:
	'subrule1' name=ID sub1=ID;

Subrule2:
	'subrule3' name=ID sub2=STRING;

Subrule3:
	'subrule3' name=ID sub1=INT;
	
CallExtendedParserRule:
	call=AbstractCallExtendedParserRule;
