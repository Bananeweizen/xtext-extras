grammar org.eclipse.xtext.index.IndexTestLanguage with org.eclipse.xtext.common.Terminals

generate indexTestLanguage "http://www.eclipse.org/xtext/indexTestLanguage"

File :
	(imports+=Import)*
	(nameSpaces+=Namespace)*;
	
Import :
	'import' importedNamespace=QualifiedNameWithWildCard;
	
QualifiedNameWithWildCard :
	QualifiedName '.*'?;
	
QualifiedName :
	ID ('.' ID)*;
	
Namespace :
	name=QualifiedName '{'
		(namedElements+=NamedElement)*
	'}';
	
NamedElement :
	Namespace | Type;
	
Type :
	Entity | Datatype;
	
Entity :
	'entity' name=ID '{'
		(properties+=Property)*
	'}';
	
Datatype :
	'datatype' name=ID;
	
Property :
	type=[Type] name=ID; 
